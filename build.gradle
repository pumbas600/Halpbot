plugins {
    id 'java'
    id 'org.cadixdev.licenser' version '0.6.1'
}

def halpBotVerson = '2.0.0'

group 'nz.pumbas.halpbot'
version halpBotVerson

repositories {
    mavenCentral()

    maven {
        url "https://m2.dv8tion.net/releases"
    }
}

test {
    useJUnitPlatform()
}

subprojects { Project subProject ->
    apply plugin: 'java'
    apply plugin: 'org.cadixdev.licenser'

    group 'nz.pumbas.halpbot'
    version halpBotVerson

    license {
        skipExistingHeaders = true
        header = rootProject.file('LICENSE')
        ignoreFailures = true
        include '**/*.java'
    }

    sourceCompatibility = 11
    targetCompatibility = 11
}

tasks.build {
    dependsOn tasks.updateLicenses
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-parameters'
    }

    plugins.withType(JavaPlugin) {
        repositories {
            mavenCentral()
            maven {
                url 'https://m2.dv8tion.net/releases'
            }
        }

        configurations {
            include
            implementation.extendsFrom(include)
            testImplementation.extendsFrom(include)
        }

        dependencies {
            include 'net.dv8tion:JDA:4.3.0_333'
            implementation 'org.apache.logging.log4j:log4j-to-slf4j:2.14.1'

            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        }

        test {
            useJUnitPlatform()
        }
    }
}