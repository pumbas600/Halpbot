plugins {
    id 'java'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'io.freefair.lombok' version '6.2.0'
}

def halpBotVerson = '2.0.4'

group 'nz.pumbas.halpbot'
version halpBotVerson

repositories {
    mavenCentral()

    maven {
        url "https://m2.dv8tion.net/releases"
    }
}

test {
    useJUnitPlatform()
}

subprojects { Project subProject ->
    apply plugin: 'java'
    apply plugin: 'org.cadixdev.licenser'
    apply plugin: 'io.freefair.lombok'

    group 'nz.pumbas.halpbot'
    version halpBotVerson

    license {
        skipExistingHeaders = true
        header = rootProject.file('LICENSE')
        ignoreFailures = true
        include '**/*.java'
    }

    sourceCompatibility = 16
    targetCompatibility = 16
}

tasks.build {
    dependsOn tasks.updateLicenses
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-parameters'
    }

    plugins.withType(JavaPlugin) {
        repositories {
            mavenCentral()
            maven {
                url 'https://m2.dv8tion.net/releases'
            }
        }

        configurations {
            include
            implementation.extendsFrom(include)
            testImplementation.extendsFrom(include)
        }

        dependencies {
            include 'net.dv8tion:JDA:4.3.0_333'
            implementation 'org.apache.logging.log4j:log4j-to-slf4j:2.14.1'
            implementation 'org.checkerframework:checker-qual:3.18.1'
            //implementation fileTree(dir: 'libs', include: '*.jar')

            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

            implementation 'org.dockbox.hartshorn:hartshorn-core:4.2.4'
            implementation 'org.dockbox.hartshorn:hartshorn-config:4.2.4'
            implementation 'org.dockbox.hartshorn:hartshorn-events:4.2.4'
            implementation 'org.dockbox.hartshorn:hartshorn-persistence:4.2.4'

            testImplementation 'org.dockbox.hartshorn:hartshorn-core:4.2.4'
        }

        test {
            useJUnitPlatform()
        }
    }
}

dependencies {
    implementation 'javax.inject:javax.inject:1'
    implementation 'org.checkerframework:checkerframework-maven-plugin:1.8.2'
    implementation 'org.checkerframework:checker-qual:3.18.1'
}