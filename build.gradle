plugins {
    id 'java'
    id 'org.cadixdev.licenser' version "0.5.0"
}

group 'nz.pumbas.halpbot'
version '1.0.1'

repositories {
    mavenCentral()

    maven {
        url "https://m2.dv8tion.net/releases"
    }
}

test {
    useJUnitPlatform()
}

subprojects { Project subProject ->
    apply plugin: "java"
    apply plugin: 'org.cadixdev.licenser'

    group 'nz.pumbas.halpbot'
    version '1.0.1'

    license {
        header = rootProject.file("LICENSE")
        ignoreFailures = true
        include '**/*.java'
    }

    sourceCompatibility = 11

    // Include HalpBot-Core in all projects that aren't the core project.
    if (!subProject.name.equals("HalpBot-Core")) {
        dependencies {
            implementation project(":HalpBot-Core")
        }
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-parameters'
    }

    plugins.withType(JavaPlugin) {
        repositories {
            mavenCentral()
            maven {
                url "https://m2.dv8tion.net/releases"
            }
        }

        configurations {
            include
            implementation.extendsFrom(include)
            testImplementation.extendsFrom(include)
        }

        dependencies {
            include "net.dv8tion:JDA:4.3.0_285"

            testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
            testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
        }

        test {
            useJUnitPlatform()
        }
    }
}